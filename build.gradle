import java.nio.file.Paths

buildscript {
    ext {
        kotlinVersion = '1.2.71'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
    }
}

ext {
    versions = ext {
        kotlin = '1.2.71'
        junit = '5.1.0'

        carbon = ext {
            validator = '0.1'
        }
    }
}

allprojects {
    group = 'org.carbon.libs'

    repositories {
        mavenCentral()
    }
}

subprojects {

    apply plugin: 'kotlin'
    apply plugin: 'maven-publish'

    sourceCompatibility = 1.8

    sourceSets {
        main {
            kotlin {
                srcDirs += 'src/main/kotlin'
            }
        }
        test {
            kotlin {
                srcDirs += 'src/tet/kotlin'
            }
        }
    }

    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
        }
    }

    compileTestKotlin {
        kotlinOptions {
            freeCompilerArgs = ["-Xjsr305=strict"]
            jvmTarget = "1.8"
        }
    }

    test {
        useJUnitPlatform {
            includeEngines 'junit-jupiter'
        }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$versions.kotlin"
        implementation "org.jetbrains.kotlin:kotlin-reflect:$versions.kotlin"

        testImplementation "org.junit.jupiter:junit-jupiter-api:$versions.junit"
        testImplementation "org.junit.jupiter:junit-jupiter-params:$versions.junit"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$versions.junit"
    }


    task sourceJar(type: Jar) {
        classifier "sources"
        from sourceSets.main.allSource
    }

    publishing {
        publications {
            mavenGithubRepo(MavenPublication) {
                from components.java
                artifact sourceJar
            }
        }
        repositories {
            maven {
                def releaseDir = Paths.get(rootDir.absolutePath, '/docs/release')
                println """
                            | save artifacts
                            | @ $releaseDir
                        """.stripMargin()
                url releaseDir.toUri()
            }
        }
    }
}
